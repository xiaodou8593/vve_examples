#vve_examples:test_car/main_c
# vve_examples:test_car/tick调用
# 实体对象主程序

tag @s remove vve_material_box
function vve_examples:test_car/_get
# 运动学迭代
execute as 0-0-0-0-0 run function vve:object/_iter_motion
# 介质探测
execute as 0-0-0-0-0 run function vve_examples:test_car/_iter_cpoints_c
# 力学迭代
scoreboard players operation vy int -= vve_gravity int
# 介质响应
#execute if score shift_response int matches 1 run function vve:object/_apply_shift
execute if score shift_response int matches 1 if score surface int matches 0 unless data storage vve:io shift_points[2] run function vve:object/_apply_shift
execute if score impulse_response int matches 1 run function vve:object/_apply_impulse_f
execute if score couple_response int matches 1 as 0-0-0-0-0 run function vve:box_object/_apply_couple
# 外部冲量
data modify storage vve:io result set from entity @s data.impulse_receiver
execute if data storage vve:io result[0] run function vve:cubox/_outer_impulse
function vve:object/_apply_friction
# 姿态角速度修正
tag @s[tag=vve_surface] remove vve_surface
execute if data storage vve:io shift_points[0] run tag @s add vve_surface
execute if data storage vve:io shift_points[2] run scoreboard players set surface int 1
execute if score surface int matches 1 as 0-0-0-0-0 run function vve_examples:test_car/_regular
execute if score surface int matches 0 if score grab_layer_response int matches 3.. if score grab_layer_receiver_v_norm int < grab_layer_regular_v int as 0-0-0-0-0 run function vve:object/_regular
# 按键控制
execute on passengers on passengers run function vve_examples:test_car/control/get_signal
execute if entity @s[tag=vve_surface] run function vve_examples:test_car/control/main_surface
execute if entity @s[tag=!vve_surface] run function vve_examples:test_car/control/main_air
# 发动机引擎
scoreboard players set res int 1
execute if score target_power int matches 0 if score damp_x int matches 0 if score damp_v int matches 0 run scoreboard players set res int 0
execute if score res int matches 1 run function vve_examples:test_car/engine/main
# 运动同步
function vve:cubox/_sync_motion
function vve_examples:test_car/_store
tag @s add vve_material_box