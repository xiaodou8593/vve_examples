#vve_examples:dice_6/main
# vve_examples:dice_6/tick调用
# 实体对象主程序

function vve_examples:dice_6/_get
# 运动学迭代
execute as 0-0-0-0-0 run function vve:object/_iter_motion
# 介质探测
execute as 0-0-0-0-0 run function vve:block/_iter_cpoints
# 力学迭代
scoreboard players operation vy int -= vve_gravity int
# 介质响应
execute if score shift_response int matches 1 run function vve:object/_apply_shift
execute if score impulse_response int matches 1 as 0-0-0-0-0 run function vve:block/_apply_impulse
function vve:object/_apply_friction
# 姿态角速度修正

## 稳定判定，改为给予自身tag
#execute if score grab_layer_response int matches 3.. if score grab_layer_receiver_v_norm int < grab_layer_regular_v int if score @s killtime matches 1.. run scoreboard players operation @s killtime < 10 int
execute if score grab_layer_response int matches 3.. if score grab_layer_receiver_v_norm int < grab_layer_regular_v int run tag @s add stable


execute if score grab_layer_response int matches 3.. if score grab_layer_receiver_v_norm int < grab_layer_regular_v int as 0-0-0-0-0 run function vve:object/_regular
# 运动同步
function vve:object/_sync_motion
function vve_examples:dice_6/_store

# 坐标安全
execute unless score y int matches -640000..5120000 run kill @s